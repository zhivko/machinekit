component wcompn "N-range window comparator";
pin in float in "Value being compared";
pin in float value#[pincount] "Boundary N for comparison";
pin in s32 default_out = -1 "Default output value when input is not in range";
pin out s32 out "Returns the index of the window the *in* is currently in";
pin out bit under "True if *in* is less than *value0*";
pin out bit over "True if *in* is greater than or equal to *value<N-1>*";
pin out bit in_range "True if *in* is strictly between *value0* and *value<N-1>*";
notes "If *max* <= *min* or *value<x>* are not in ascending order then the behavior is undefined.";

instanceparam int pincount = 2;

option MAXCOUNT 16;

function _ fp;
license "GPL";
;;
FUNCTION(_)
{
    hal_s32_t i;

    out = default_out;
    for (i=1; i < localpincount; i++)
    {
        if ((in >= value(i-1)) && (in < value(i)))
        {
            out = i-1;
        }
    }
    under = (in < value(0));
    over = (in >= value(localpincount-1));
    in_range = !(under || over);

    return 0;
}
